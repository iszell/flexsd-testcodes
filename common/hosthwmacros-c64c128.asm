;------------------------------------------------------------------------------
;---	Host HW handler macros for C64/C128
;---	Â©2024.08.14.+ by BSZ
;------------------------------------------------------------------------------
;	Port to output:
pp_porttoout MACRO
		lda	#%11111111
		sta	$dd01			;CIA PORTB - All PP lines High
		sta	$dd03			;CIA DDRB - All PP lines to output
    ENDM
;	Port to input:
pp_porttoin MACRO
		lda	#%00000000
		sta	$dd03			;CIA DDRB - All PP lines to input
    ENDM

;	Write data to port:
pp_writeport MACRO
		sta	$dd01			;CIA PORTB - set data to PP, HRWP generated by CIA itself
    ENDM
;	Read data from port:
pp_readport MACRO
		lda	$dd01			;CIA PORTB - read PP lines, HRWP generated by CIA itself
    ENDM

;	Wait drive's R/W port pulse:
pp_waitdrwp MACRO
		lda	#%00010000		;_FLAG line
		bit	$dd0d			;CIA ICR: Active edge detected?
		beq	*-3			;Wait DRWP
    ENDM
;	Check drive's R/W port flag:
pp_chkdrwp MACRO
		lda	#%00010000		;_FLAG line
		bit	$dd0d			;CIA ICR: Active edge detected?
    ENDM
;	Clear drive's R/W port flag
pp_clrdrwp MACRO
		bit	$dd0d			;CIA ICR read, clear DRWP (and all other) flag
    ENDM

;	HW init:
pp_hwinit MACRO
		pp_porttoin			;Set port direction to input
		bit	$dd0d			;CIA ICR read, clear DRWP flag
    ENDM
;	HW deinit:
pp_hwdeinit MACRO
		pp_porttoin
    ENDM

;	Wait for CLK line low:
ser_waitclklo MACRO
		bit	$dd00			;CIA PORTA, CLK line -> V
		bvs	*-3
    ENDM

;	Set DAT line drive:
ser_setdat MACRO lev
      IF lev == 0
		lda	#def_cia_vicbank | %00100000	;Release CLK (ATN), Drive DAT
      ELSE
		lda	#def_cia_vicbank | %00000000	;Release CLK, DAT (ATN)
      ENDIF
		sta	$dd00				;CIA PORTA
    ENDM

;	Get CLK+DAT line state:
ser_getclkdat MACRO
		lda	$dd00
		cmp	$dd00
		bne	*-6
		asl	a			;DAT -> Cy, CLK -> N
    ENDM
;------------------------------------------------------------------------------
