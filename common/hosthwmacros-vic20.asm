;------------------------------------------------------------------------------
;---	Host HW handler macros for VIC20
;---	Â©2024.08.14.+ by BSZ
;------------------------------------------------------------------------------
;	Port to output:
pp_porttoout MACRO
		lda	#%11111111
		sta	$9110			;VIA1 PORTB - All PP lines High
		sta	$9112			;VIA1 DDRB - All PP lines to output
    ENDM
;	Port to input:
pp_porttoin MACRO
		lda	#%00000000
		sta	$9112			;VIA1 DDRB - All PP lines to input
    ENDM

;	Write data to port:
pp_writeport MACRO
		sta	$9110			;VIA PORTB - set data to PP, HRWP generated by VIA itself
    ENDM
;	Read data from port:
pp_readport MACRO
		lda	$9110			;VIA PORTB - read PP lines
		sta	$9110			;VIA PORTB - write back, PORTB generate HRWP is port write only
    ENDM

;	Wait drive's R/W port pulse:
pp_waitdrwp MACRO
		lda	#%00010000		;CB1 line
		bit	$911d			;Active edge detected?
		beq	*-3			;Wait DRWP
    ENDM
;	Check drive's R/W port flag:
pp_chkdrwp MACRO
		lda	#%00010000		;CB1 line
		bit	$911d			;VIA1 CB1 Active edge detected?
    ENDM
;	Clear drive's R/W port flag
pp_clrdrwp MACRO
		bit	$9110			;VIA1 read PORTB, clear DRWP flag
    ENDM

;	HW init:
pp_hwinit MACRO
		pp_porttoin			;Set port direction to input
		lda	$911c			;VIA1 PCR
		and	#%00001111		;
		ora	#%10100000		;%101: CB2 Pulse mode, %0: CB1 Negative active edge
		sta	$911c
		bit	$9110			;VIA1 PORTB, clear flag
    ENDM
;	HW deinit:
pp_hwdeinit MACRO
		pp_porttoin
		lda	$911c			;VIA1 PCR
		and	#%00001111		;%000: CB2 Input
		sta	$911c
    ENDM

;	Wait for CLK line low:
ser_waitclklo MACRO
		lda	#%00000001		;CLK line
		bit	$911f			;VIA1 DRA
		bne	*-3
    ENDM

;	Set DAT line drive:
ser_setdat MACRO lev
      IF lev == 0
		lda	#%11111100			;Release CLK, Drive DAT
      ELSE
		lda	#%11011100			;Release CLK + DAT
      ENDIF
		sta	$912c				;VIA2 PCR
    ENDM

;	Get CLK+DAT line state:
ser_getclkdat MACRO
		lda	$911f
		cmp	$911f
		bne	*-6
		lsr	a
		ror	a		;DAT -> Cy, CLK -> N
    ENDM
;------------------------------------------------------------------------------
